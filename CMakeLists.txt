CMAKE_MINIMUM_REQUIRED(VERSION 2.6.0)

PROJECT(hydrogen-drumkits)

SET(CMAKE_MODULE_PATH ${CMAKE_SOURCE_DIR}/cmake/modules ${CMAKE_MODULE_PATH})

SET(VERSION_MAJOR "0")
SET(VERSION_MINOR "4")
SET(VERSION_PATCH "8")
#SET(VERSION_SUFFIX "")

SET(VERSION "${VERSION_MAJOR}.${VERSION_MINOR}.${VERSION_PATCH}")
IF(VERSION_SUFFIX)
	SET(VERSION "${VERSION}-${VERSION_SUFFIX}")
ENDIF(VERSION_SUFFIX)

INCLUDE(DetectMachine)

MACRO(INSTALL_DRUMKIT _drumkit _wildcards)
	STRING(REPLACE " " "_" DRUMKIT "${_drumkit}")
	STRING(REPLACE "-" "_" DRUMKIT_COMPONENT "DrumKit_${DRUMKIT}")
	STRING(TOUPPER ${DRUMKIT_COMPONENT} CNAME)
	SET(CPACK_COMPONENT_${CNAME}_DISPLAY_NAME ${_drumkit})
	SET(CPACK_COMPONENTS_ALL ${CPACK_COMPONENTS_ALL} ${DRUMKIT_COMPONENT})

	FOREACH(_wildcard ${_wildcards})
		FILE(GLOB_RECURSE files "${DRUMKIT}/${_wildcard}")
		FOREACH(_file ${files})
			MESSAGE("${DRUMKIT}: ${DRUMKIT_COMPONENT}: ${_file}")
			IF(LMMS_BUILD_WIN32)
				INSTALL(FILES "${_file}" DESTINATION "${_drumkit}/" COMPONENT ${DRUMKIT_COMPONENT})
			ELSE(LMMS_BUILD_WIN32)
				INSTALL(FILES "${_file}" DESTINATION "${LMMS_DATA_DIR}/samples/drumkits/${_drumkit}/" COMPONENT ${DRUMKIT_COMPONENT})
			ENDIF(LMMS_BUILD_WIN32)
		ENDFOREACH(_file ${files})
	ENDFOREACH(_wildcard ${_wildcards})
ENDMACRO(INSTALL_DRUMKIT)


# add/install actual drumkits
SET(DRUMKITS 3355606kit BJA_Pacific "Boss DR-110" "circAfrique v4" Classic-626 Classic-808 ColomboAcousticDrumkit DeathMetal EasternHop-1 ElectricEmpireKit ErnysPercussion HardElectro1 HipHop-1 HipHop-2 K-27_Trash_Kit Millo-Drums_v.1 Millo_MultiLayered2 Millo_MultiLayered3 "Roland TR-606" "Roland TR-626" "Roland TR-707" "Roland TR-808" "Roland TR-909" Synthie-1 TD-7kit Techno-1 TR808909 VariBreaks YamahaVintageKit)


FOREACH(drumkit ${DRUMKITS})
	INSTALL_DRUMKIT(${drumkit} "*.flac;*.html;drumkit.xml;COPYING;README;LICENCE")
ENDFOREACH(drumkit ${DRUMKITS})


#
# add distclean-target
#
ADD_CUSTOM_TARGET(distclean
			COMMAND make clean
			COMMAND rm -rf `find -name cmake_install.cmake` `find -name Makefile` `find -type d -name CMakeFiles` CMakeCache.txt)

#
# add tarball-target
#
SET(TMP "hydrogen-drumkits-${VERSION}")
ADD_CUSTOM_TARGET(dist
			COMMAND make clean
			COMMAND rm -rf ${TMP}
			COMMAND mkdir -p ${TMP}
			COMMAND cp AUTHORS build_mingw32 CMakeLists.txt COPYING INSTALL README TODO ${TMP}
			COMMAND find -type d -exec cp -r '{}' ${TMP}
			COMMAND find ${TMP} -name cmake_install.cmake -delete
			COMMAND find ${TMP} -name Makefile -delete
			COMMAND find ${TMP} -type d -name CMakeFiles -delete
			COMMAND tar cjf ${TMP}.tar.bz2 ${TMP}
			COMMAND rm -rf ${TMP})


INCLUDE(InstallRequiredSystemLibraries)
SET(CPACK_PACKAGE_DESCRIPTION_SUMMARY "Hydrogen Drumkits for LMMS")
SET(CPACK_PACKAGE_VENDOR "LMMS Developers")
IF(LMMS_BUILD_APPLE)
CONFIGURE_FILE(${CMAKE_SOURCE_DIR}/README ${CMAKE_BINARY_DIR}/README.txt COPYONLY)
CONFIGURE_FILE(${CMAKE_SOURCE_DIR}/COPYING ${CMAKE_BINARY_DIR}/COPYING.txt COPYONLY)
SET(CPACK_PACKAGE_DESCRIPTION_FILE "${CMAKE_BINARY_DIR}/README.txt")
SET(CPACK_RESOURCE_FILE_LICENSE "${CMAKE_BINARY_DIR}/COPYING.txt")
ELSE(LMMS_BUILD_APPLE)
SET(CPACK_PACKAGE_DESCRIPTION_FILE "${CMAKE_SOURCE_DIR}/README")
SET(CPACK_RESOURCE_FILE_LICENSE "${CMAKE_SOURCE_DIR}/COPYING")
ENDIF(LMMS_BUILD_APPLE)

SET(CPACK_PACKAGE_VERSION_MAJOR "${VERSION_MAJOR}")
SET(CPACK_PACKAGE_VERSION_MINOR "${VERSION_MINOR}")
SET(CPACK_PACKAGE_VERSION_PATCH "${VERSION_PATCH}")
IF(VERSION_SUFFIX)
	SET(CPACK_PACKAGE_VERSION_PATCH "${VERSION_PATCH}-${VERSION_SUFFIX}")
ENDIF(VERSION_SUFFIX)
IF(WIN32)
SET(CPACK_NSIS_DISPLAY_NAME "Hydrogen Drumkits for LMMS")
SET(CPACK_NSIS_HELP_LINK "http:\\\\\\\\lmms.sourceforge.net")
SET(CPACK_NSIS_URL_INFO_ABOUT "http:\\\\\\\\lmms.sourceforge.net")
SET(CPACK_NSIS_CONTACT "lmms-devel@lists.sourceforge.net")
SET(CPACK_PACKAGE_FILE_NAME "hydrogen-drumkits-for-lmms-${VERSION}-win32")
SET(CPACK_PACKAGE_INSTALL_DIRECTORY "LMMS ${VERSION}\\\\${LMMS_DATA_DIR}\\\\samples\\\\drumkits")
ELSE(WIN32)
SET(CPACK_PACKAGE_INSTALL_DIRECTORY "LMMS ${VERSION}/${LMMS_DATA_DIR}/samples/drumkits")
ENDIF(WIN32)

SET(MACOSX_BUNDLE_GUI_IDENTIFIER "LMMS")
SET(MACOSX_BUNDLE_LONG_VERSION_STRING "${VERSION}")
SET(MACOSX_BUNDLE_BUNDLE_NAME "Hydrogen Drumkits for LMMS")
SET(MACOSX_BUNDLE_SHORT_VERSION_STRING "${VERSION}")
SET(MACOSX_BUNDLE_BUNDLE_VERSION "${VERSION}")
SET(MACOSX_BUNDLE_COPYRIGHT "LMMS Developers, 2010")

SET(CPACK_SOURCE_GENERATOR "TBZ2")
SET(CPACK_SOURCE_PACKAGE_FILE_NAME "lmms-${VERSION}")
INCLUDE(CPack)

